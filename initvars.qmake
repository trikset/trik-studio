include($$PWD/global.pri)
TEMPLATE=subdirs
SUBDIRS=
QMAKE_SUBSTITUTES += .qmake.cache.in
DISTFILES += .qmake.cache.in

GLOBAL_DESTDIR = $$absolute_path($$GLOBAL_OUTPWD/bin)
cache(GLOBAL_DESTDIR, set super)

# ATM we use xenial 16.04 as main linux builder, and it lacks new git with --broken option.
REPO_GIT_VERSION_STRING=$$system(git -C $$PWD -c $$system_quote(safe.directory=*) describe --tags --always --dirty --abbrev=6 || true)
REPO_GIT_VERSION_LIST=$$replace(REPO_GIT_VERSION_STRING, [-.], ":")
REPO_GIT_VERSION_LIST=$$replace(REPO_GIT_VERSION_LIST, [a-zA-Z], "")
REPO_GIT_VERSION_LIST=$$split(REPO_GIT_VERSION_LIST, :)

PROJECT_GIT_VERSION_MAJOR=$$member(REPO_GIT_VERSION_LIST, 0)
PROJECT_GIT_VERSION_MINOR=$$member(REPO_GIT_VERSION_LIST, 1)
PROJECT_GIT_VERSION_PATCH=$$member(REPO_GIT_VERSION_LIST, 2)
PROJECT_GIT_VERSION_BUILD=$$member(REPO_GIT_VERSION_LIST, 3)
contains(REPO_GIT_VERSION_STRING, .*dirty.*): PROJECT_GIT_VERSION_BUILD = 9999

isEmpty(PROJECT_GIT_VERSION_BUILD):PROJECT_GIT_VERSION_BUILD=0
isEmpty(PROJECT_GIT_VERSION_PATCH):PROJECT_GIT_VERSION_PATCH=0
isEmpty(PROJECT_GIT_VERSION_MINOR):PROJECT_GIT_VERSION_MINOR=0
isEmpty(PROJECT_GIT_VERSION_MAJOR):PROJECT_GIT_VERSION_MAJOR=0

cache(PROJECT_GIT_VERSION_BUILD, set super)
cache(PROJECT_GIT_VERSION_PATCH, set super)
cache(PROJECT_GIT_VERSION_MINOR, set super)
cache(PROJECT_GIT_VERSION_MAJOR, set super)

TRIK_STUDIO_VERSION=$$REPO_GIT_VERSION_STRING
isEmpty(TRIK_STUDIO_VERSION):TRIK_STUDIO_VERSION=UNDEFINED.999

message(Version is "$$TRIK_STUDIO_VERSION with codes" \
      $$PROJECT_GIT_VERSION_MAJOR/$$PROJECT_GIT_VERSION_MINOR/$$PROJECT_GIT_VERSION_PATCH/$$PROJECT_GIT_VERSION_BUILD)
cache(TRIK_STUDIO_VERSION, set super)

#obsolete???
TRIK_STUDIO_YEAR = 2021
cache(TRIK_STUDIO_YEAR, set super)

########
cache(CONFIG, set super)
